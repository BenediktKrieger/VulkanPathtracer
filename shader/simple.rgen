#version 460
#extension GL_EXT_ray_tracing : enable

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;
layout(binding = 2, set = 0, rgba32f) uniform image2D accImage;

layout( push_constant ) uniform constants
{
	mat4 invProj;
	mat4 invView;
	mat4 model;
	uint accumulatedFrames;
} PushConstants;

struct RayPayload {
	vec3 color;
	vec3 attenuation;
	vec3 origin;
	vec3 dir;
	uint recursion;
  	bool shadow;
};

layout(location = 0) rayPayloadEXT RayPayload Payload;

float halton(uint base, uint index)
{
	float result = 0.;
	float f = 1.;
	while (index > 0)
	{
		f = f / float(base);
		result += f * float(index % base);
		index = index / base;
	}
	return result;
}

void main() 
{
	vec4 origin = PushConstants.invView * vec4(0,0,0,1);
	uint accFrames = PushConstants.accumulatedFrames;

	float tmin = 0.0001;
	float tmax = 10000.0;
	uint spp = 1;

	vec3 sumofHitValues = vec3(0.0);

	uint offset = (gl_LaunchIDEXT.y * gl_LaunchIDEXT.x + gl_LaunchIDEXT.x) * spp + accFrames;
	for(uint i = 0; i < spp; i++){
		uint haltonIndex = offset + i;
		vec2 haltonValue = (vec2(halton(2, haltonIndex), halton(3, haltonIndex)) - .5);
		const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + haltonValue;
		const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
		vec2 d = inUV * 2.0 - 1.0;
		vec4 target = PushConstants.invProj * vec4(d.x, d.y, 1, 1);
		vec4 direction = PushConstants.invView * vec4(normalize(target.xyz), 0);
		
		Payload.color = vec3(0.0);
		Payload.recursion = 0;
		Payload.attenuation = vec3(1.0);
		Payload.origin = origin.xyz;
		Payload.dir = direction.xyz;
		
		while(Payload.recursion <= 5) {
			traceRayEXT(topLevelAS, gl_RayFlagsNoneEXT, 0xFF, 0, 0, 0, Payload.origin, tmin, Payload.dir, tmax, 0);
		}

		sumofHitValues += Payload.color;
	}

	vec4 oldColor = imageLoad(accImage, ivec2(gl_LaunchIDEXT.xy));
	
	vec3 newColor = sumofHitValues / spp;

	if(accFrames > 1) {
		newColor = oldColor.xyz * ((accFrames - 1.0) /  accFrames) + newColor * (1.0 / accFrames);
	}

	imageStore(accImage, ivec2(gl_LaunchIDEXT.xy), vec4(newColor, 1.0));
	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(newColor, 1.0));
}
